buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

version = "1.12.2-" + getVersion()
group = "p455w0rd"
archivesBaseName = "DeathlyChambers"
sourceCompatibility = targetCompatibility = "1.8"

minecraft {
	version = "1.12.2-14.23.4.2739"
	runDir = "run"
	mappings = "snapshot_20171003"
	useDepAts = true
}

repositories {
	
	maven {
		name = "progwml6"
		url = "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name = "tterrag"
		url = "http://maven.tterrag.com"
	}
	maven {
		url "http://mobiusstrip.eu/maven"
	}
	maven {
		 name = "epoxide"
		 url = "http://maven.epoxide.xyz"
	 }
	 maven {
		name = "tehnut"
		url = "http://tehnut.info/maven"
	}
	maven {
        name = "covers Maven"
        url = "http://maven.covers1624.net"
    }
}
dependencies {
	deobfCompile "mezz.jei:jei_1.12.2:+"
	//compile project(':appliedenergistics2')
	//deobfCompile "com.enderio:EnderIO:1.10.2-3.0.1.112_beta"
	//deobfCompile "com.enderio.core:EnderCore:1.10.2-0.4.1.55-beta"
	//deobfCompile "mcp.mobius.waila:Waila:1.7.0-B3_1.9.4"
	//deobfCompile "net.darkhax.tesla:Tesla-1.12:1.0.61"
	deobfCompile "mcp.mobius.waila:Hwyla:1.8.20-B35_1.12"
	//deobfCompile "appeng:appliedenergistics2:rv6-alpha-2"
	//deobfCompile "cofh:RedstoneFlux:1.12-2.0.1.2:universal"
}

task apiJar(type: Jar) {
	classifier = 'api'
	from sourceSets.api.output
	from sourceSets.api.allJava
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

artifacts {
	//archives apiJar
	archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven") || (project.hasProperty("maven_user") && project.hasProperty("maven_pass"))) {
				if (project.hasProperty("maven_pass")) {
					println "Uploading to remote maven."
					repository(url: "http://maven_upload.covers1624.net/") {
						authentication(userName: project.getProperty("maven_user"), password: project.getProperty("maven_pass"))
					}
				} else {
					println "Uploading to local maven."
					repository(url: "file://${local_maven}/.m2/repository")
				}
                pom {
                    groupId = project.group
                    // Force the maven upload to use the <mcversion>-<version> syntax preferred at files
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'DeathlyChambers'
                        url 'https://github.com/p455w0rd/DeathlyChambers'

                        scm {
                            url 'https://github.com/p455w0rd/DeathlyChambers'
                            connection 'scm:git:git://github.com/p455w0rd/DeathlyChambers.git'
                            developerConnection 'scm:git:git@github.com:p455w0rd/DeathlyChambers.git'
                        }

                        issueManagement {
                            system 'github'
                            url 'https://github.com/p455w0rd/DeathlyChambers/issues'
                        }

                        licenses {
                            license {
                                name 'MIT'
                                url 'https://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'TheRealp455w0rd'
                                name 'Mike Balser'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
    }
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include "mcmod.info"
		expand "version": project.version, "mcversion": project.minecraft.version
	}
		
	from (sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
	}
}

String getVersion() {
	String major = "0";
	String revision = "0";
	String patch = "0";
	String prefix = "public static final String VERSION = \"";
	File file = file("src/main/java/p455w0rd/deathlychambers/init/ModGlobals.java")
	file.eachLine { String s ->
		s = s.trim();
		if (s.startsWith(prefix)) {
			s = s.substring(prefix.length(), s.length() - 2);
			String[] pts = s.split("\\.");

			major = pts[0];
			revision = pts[1];
			patch = pts[2];
		}
	}
	return "$major.$revision.$patch";
}
